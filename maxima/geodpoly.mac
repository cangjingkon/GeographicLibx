/* Print out the coefficients for the geodesic series in a format that
allows Math::polyval to be used. */
load("geod8.lsp")$
taylordepth:5$
simplenum:true$
count:0$
jtaylor(expr,var1,var2,ord):=expand(subst([zz=1],
    ratdisrep(taylor(subst([var1=zz*var1,var2=zz*var2],expr),zz,0,ord))))$
h(x):=if x=0 then 0 else block([n:0],while integerp(x/16) do (x:x/16,n:n+1),n);
formatnum(x):=block([n:h(x)],
  if n>4 then (x:x/16^n,n:concat("<<",4*n)) else n:"",
  concat(if x=0 then "0"
  else if abs(x)<1000000 then string(x)
  else concat(if x<0 then "-" else "",
    "0x",block([obase:16,s],
      s:sdowncase(string(abs(x))),
      if substring(s,1,2) = "0" then s:substring(s,2),
      s)),"LL",n))$
formatnumx(x):=if simplenum then
concat(string(x),if abs(x) < 2^32 then "" else "LL") else
if abs(x)<2^63
then concat("real(",formatnum(x),")") else
concat("reale(",formatnum(floor(x/2^52)),",",
  formatnum(x-floor(x/2^52)*2^52),")")$
printterm(x,line):=block([lx:slength(x)+1,lline:slength(line)],
  count:count+1,
  x:concat(x,if simplenum then ", " else ","),
  if lline=0 then line:concat("      ",x)
  else (if lx+lline<80 then line:concat(line,x)
    else (print(line),line:concat("      ",x))),
  line)$
flushline(line):=(if slength(line)>0 then (print(line),line:""),line)$
polyprint(p, var, title):=block([linel:90,d,line:"",h],
  p:ratsimp(p),
  d:abs(denom(p)),
  p:expand(d*p),
  h:hipow(p,var),
  print(concat("      // ", title, ", polynomial in ", var, " of order ",h)),
  for k:h step -1 thru 0 do
  line:printterm(formatnumx(coeff(p,var,k)),line),
  line:printterm(formatnumx(d),line),
  flushline(line),
  done)$

value1(val):=block([tab2:"    ",linel:90,macro],
  macro:if simplenum then "GEOGRAPHICLIB_GEODESIC_ORDER"
  else "GEOGRAPHICLIB_GEODESICEXACT_ORDER",
  print(concat(tab2,"// Generated by Maxima on ",timedate())),
  for nn:0 step 2 thru maxpow do block([c],
    if nn = 0 then
    print(concat("#if ", macro, "/2 == ",nn/2))
    else
    print(concat("#elif ", macro, "/2 == ",nn/2)),
    count:0,
    print(concat(tab2,"static const real coeff[] = {")),
    block(
      [q:ratdisrep(taylor(ev(val),eps,0,nn)),line:""],
      q:subst(eps=sqrt(eps2),expand(q)),
      polyprint(q,eps2,string(val)),
      line:flushline(line)),
    print(concat(tab2,"};  // count = ",count))),
  print("#else
#error", concat("\"Bad value for ", macro, "\""), "
#endif
"),
'done)$

array1(array):=block([tab2:"    ",linel:90,macro],
  macro:if simplenum then "GEOGRAPHICLIB_GEODESIC_ORDER"
  else "GEOGRAPHICLIB_GEODESICEXACT_ORDER",
  print(concat(tab2,"// Generated by Maxima on ",timedate())),
  for nn:0 thru maxpow do block([c],
    if nn = 0 then
    print(concat("#if ", macro, " == ",nn))
    else
    print(concat("#elif ", macro, " == ",nn)),
    count:0,
    print(concat(tab2,"static const real coeff[] = {")),
    for m:0 thru nn do if part(arrayapply(array,[m]),0) # array then block(
      [q:ratdisrep(taylor(arrayapply(array,[m]),eps,0,nn)),line:""],
      q:subst(eps=sqrt(eps2),expand(q/eps^m)),
      polyprint(q,eps2,concat(array, "[", m, "]/eps^",m)),
      line:flushline(line)),
    print(concat(tab2,"};  // count = ",count))),
  print("#else
#error", concat("\"Bad value for ", macro, "\""), "
#endif
"),
'done)$

value2(val):=block([tab2:"    ",linel:90,macro],
  macro:if simplenum then "GEOGRAPHICLIB_GEODESIC_ORDER"
  else "GEOGRAPHICLIB_GEODESICEXACT_ORDER",
  print(concat(tab2,"// Generated by Maxima on ",timedate())),
  for nn:0 thru maxpow do block([c],
    if nn = 0 then
    print(concat("#if ", macro, " == ",nn))
    else
    print(concat("#elif ", macro, " == ",nn)),
    count:0,
    print(concat(tab2,"static const real coeff[] = {")),
    block(
      [q:jtaylor(ev(val),n,eps,nn-1),line:""],
      for j:nn-1 step -1 thru 0 do block(
        [p:coeff(q,eps,j)],
        polyprint(p,n,concat(val, ", coeff of eps^",j))),
      line:flushline(line)),
    print(concat(tab2,"};  // count = ",count))),
  print("#else
#error", concat("\"Bad value for ", macro, "\""), "
#endif
"),
'done)$

array2(array):=block([tab2:"    ",linel:90,macro],
  macro:if simplenum then "GEOGRAPHICLIB_GEODESIC_ORDER"
  else "GEOGRAPHICLIB_GEODESICEXACT_ORDER",
  print(concat(tab2,"// Generated by Maxima on ",timedate())),
  for nn:0 thru maxpow do block([c],
    if nn = 0 then
    print(concat("#if ", macro, " == ",nn))
    else
    print(concat("#elif ", macro, " == ",nn)),
    count:0,
    print(concat(tab2,"static const real coeff[] = {")),
    for m:0 thru nn-1 do if part(arrayapply(array,[m]),0) # array then block(
      [q:jtaylor(arrayapply(array,[m]),n,eps,nn-1),line:""],
      for j:nn-1 step -1 thru m do block(
        [p:coeff(q,eps,j)],
        polyprint(p,n,concat(array, "[", m ,"], coeff of eps^",j))),
      line:flushline(line)),
    print(concat(tab2,"};  // count = ",count))),
  print("#else
#error", concat("\"Bad value for ", macro, "\""), "
#endif
"),
'done)$

printall():= (
  value1('(A1*(1-eps)-1)),
  array1('C1),
  array1('C1p),
  value1('(A2/(1-eps)-1)),
  array1('C2),
  value2('A3),
  array2('C3),
  array2('C4))$
