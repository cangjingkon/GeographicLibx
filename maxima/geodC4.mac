load("geod30.lsp")$
taylordepth:5$
jtaylor(expr,var1,var2,ord):=expand(subst([zz=1],
    ratdisrep(taylor(subst([var1=zz*var1,var2=zz*var2],expr),zz,0,ord))))$
codeC4():=block([tab2:"    ",tab3:"      "],
  print(concat("  // Generated by Maxima on ",timedate())),
  print("  // The coefficients n^k in C4[l] in the Fourier expansion of I4
  const Math::real* GeodesicExact::rawC4coeff() {"),
  for nn:0 thru maxpow do block([c],
    if nn = 0 then
    print(concat("#if GEOGRAPHICLIB_GEODESICEXACT_ORDER == ",nn))
    else
    print(concat("#elif GEOGRAPHICLIB_GEODESICEXACT_ORDER == ",nn)),
    print(concat(tab2,"static const real coeff[] = {")),
    c:0,
    for m:0 thru nn-1 do block(
      [q:jtaylor(subst([n=_n],C4[m]),_n,eps,nn-1)],
      for j:m thru nn-1 do block(
        [p:ratsimp(coeff(q,eps,j)),den],
        d:abs(denom(p)),
        p:expand(d*p),
        print(concat(tab3,"// _C4x[",c,"]")),
        for k:nn-1-j step -1 thru 0 do block(
          [t:coeff(p,_n,k),s:""],
          if t < 0 then (s:"-",t:-t),
          if t < 2^52 then print(concat(tab3,"real(",s,t,"LL),"))
          else print(concat(tab3,"reale(",s,floor(t/2^52),"LL,",
              s,mod(t,2^52),"LL),"))),
        if d < 2^52 then print(concat(tab3,"real(",d,"LL),"))
        else print(concat(tab3,"reale(",floor(d/2^52),"LL,",
            mod(d,2^52),"LL),")),
        c:c+1)),
    print(concat(tab2,"};"))),
  print("#else
#error \"Bad value for GEOGRAPHICLIB_GEODESICEXACT_ORDER\"
#endif
    return coeff;
  }
"),
'done)$
